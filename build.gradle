plugins {
  id 'java'
  id 'java-library'
  id 'jacoco'
  id 'maven'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'org.sonarqube' version '2.6.2'
}

group = 'com.amdelamar'
version = '1.3.0'
description = 'OTP (One Time Password) utility in Java. To enable two-factor authentication (2FA) using HMAC-based) or Time-based algorithms.'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
  jcenter()
}

dependencies {
  compile 'commons-codec:commons-codec:1.8'
  testCompile 'junit:junit:4.12'
}

test {
  testLogging {
    showStandardStreams = true
  }
}

jar {
  baseName = 'jotp'
  from sourceSets.main.allSource
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compile
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact (sourcesJar) {
        classifier = "sources"
      }
      artifact (javadocJar) {
        classifier = "javadoc"
      }
      version this.version
      groupId this.group
      artifactId 'jotp'
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['MyPublication']
  publish = true
  override = true
  pkg {
    repo = 'mvn'
    name = 'jotp'
    desc = this.description
    licenses = ['Apache-2.0']
    websiteUrl = 'https://amdelamar.com/jotp/'
    issueTrackerUrl = 'https://github.com/amdelamar/jotp/issues'
    vcsUrl = 'git://github.com/amdelamar/jotp.git'
    version {
        name = this.version
        desc = this.description
        released  = new Date()
    }
    githubRepo = 'amdelamar/jotp' // Optional Github repository
    githubReleaseNotesFile = 'README.md' // Optional Github release notes
    labels = ['otp', '2fa', 'totp', 'hotp']
    publicDownloadNumbers = true
  }
}
