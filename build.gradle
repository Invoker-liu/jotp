plugins {
  id 'java'
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
}

group = 'com.amdelamar'
version = '1.3.0'
description = 'OTP (One Time Password) utility in Java. To enable two-factor authentication (2FA) using HMAC-based) or Time-based algorithms.'
compileJava.options.encoding = 'UTF-8'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenCentral()
}

dependencies {
  implementation 'commons-codec:commons-codec:1.8'
  testImplementation 'junit:junit:4.12'
}

test {
  testLogging {
    showStandardStreams = true
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }
  finalizedBy jacocoTestReport
}

java {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  withJavadocJar()
  withSourcesJar()
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    html.required = false
  }
}

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv('SONATYPE_USERNAME')
        password = project.hasProperty('sonatypePassword') ? sonatypePassword : System.getenv('SONATYPE_PASSWORD')
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'jotp'
        artifactId = 'jotp'
        groupId = this.group
        version = this.version
        description = this.description
        url = 'https://amdelamar.com/jotp/'
        from components.java
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = "amdelamar"
            name = "Austin Delamar"
            email = "amdelamar@protonmail.com"
          }
        }
        scm {
          connection = 'scm:git:git@github.com:amdelamar/jotp.git'
          developerConnection = 'scm:git:git@github.com:amdelamar/jotp.git'
          url = 'https://github.com/amdelamar/jotp/'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}
