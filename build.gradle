plugins {
  id 'java'
  id 'java-library'
  id 'jacoco'
  id 'maven'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.0'
  id 'org.sonarqube' version '2.6.2'
}

group = 'com.github.amdelamar'
version = '1.1.0'
description = 'OTP (One Time Password) utility in Java'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
  jcenter()
}

dependencies {
  implementation 'commons-codec:commons-codec:1.8'
  
  testCompile 'junit:junit:4.12'
}

jar {
  baseName = 'jotp'
  from sourceSets.main.allSource
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compile
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['MyPublication']
  publish = true
  override = true
  pkg {
    repo = 'maven'
    name = 'jotp'
    desc = 'OTP (One Time Password) utility in Java. To enable two-factor authentication (2FA) using HMAC-based) or Time-based algorithms.'
    licenses = ['Apache-2.0']
    websiteUrl = 'https://github.com/amdelamar/jotp/'
    issueTrackerUrl = 'https://github.com/amdelamar/jotp/issues'
    vcsUrl = 'https://github.com/amdelamar/jotp.git'
    githubRepo = 'amdelamar/jotp' // Optional Github repository
    githubReleaseNotesFile = 'README.md' // Optional Github release notes
    labels = ['otp', '2fa', 'totp', 'hotp']
    publicDownloadNumbers = true
  }
}
